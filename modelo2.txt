
# Localização p-medianas

# Conjuntos

set I;                      # Candidatos

set J;                      # Clientes



# Parâmetros

param c{i in I, j in J};	# custo de transporte da facilidade i no cliente j
param p;			     # número de facilidades a serem abertas
param d{j in J};         # Demanda de cada área
param e;				# Capacidade maxima

# Variáveis

var x{i in I, j in J} >= 0, binary;# 1, sea área j é atendida pela facilidade i
						# 0, caso contrário

var y{i in I} >= 0, binary;	#1, se a facilidade é aberta em i
				#0, caso contrário


# Função objetivo: minimizar a distância entre microáreas e psfs

minimize FO: sum{i in I, j in J} d[j]*c[i,j]*x[i,j];


# Restrições

# Atendimento de uma região por um psf

s.t. R1{j in J}: sum{i in I} x[i,j] = 1;


# Apenas uma facilidade aberta pode atender uma microarea

s.t. R2{i in I, j in J}: x[i,j] <= y[i]; 


# Número de psfs a serem abertas
s.t. R3: sum{i in I} y[i] = p;

# Defini a capacidade 
  s.t. R4{i in I}: sum {j in J} d[j]*x[i,j] <= e ;

end;

